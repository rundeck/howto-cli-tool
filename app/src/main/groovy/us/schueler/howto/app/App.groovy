/*
 * This Groovy source file was generated by the Gradle 'init' task.
 */
package us.schueler.howto.app

import groovy.transform.CompileStatic
import groovy.util.logging.Slf4j
import picocli.CommandLine
import us.schueler.howto.Howto


@CompileStatic
@CommandLine.Command(
        name = "howto",
        versionProvider = VersionProvider,
        mixinStandardHelpOptions = true, // add --help and --version options
        description = """Discover how to do things and do them""")
@Slf4j
class App {
    static void main(String[] args) {
        if (args.length > 0 && args[0] !in ['-V', '--version', '-h', '--help', 'help', 'ls', 'list', 'run', 'do', 'to']) {
            //assume a run command is intended
            args = new ArrayList<String>(['run'] + args.toList()).toArray(new String[]{})
        } else if (args.length == 0) {
            args = ['help'].toArray(new String[1])
        }
        System.exit(new CommandLine(new App()).execute(args))
    }


    @CommandLine.Command(name = 'help', aliases = ['list', 'ls', 'to'], description = 'List available actions')
    int help(
            @CommandLine.Option(names = ['-d', '--dir'], description = 'Base dir') File baseDir,
            @CommandLine.Option(names = ['-v', '--verbose'], description = 'Verbose') boolean verbose,
            @CommandLine.Option(names = ['-a', '--all'], description = 'All') boolean all,
            @CommandLine.Parameters(paramLabel = 'args', description = "args passed to the action") List<String> args
    ) {
        run(baseDir, args?.size() > 0 || verbose, all, 'help', args)
    }

    @CommandLine.Command(name = 'run', aliases = ['do'], description = 'Run an action')
    int run(
            @CommandLine.Option(names = ['-d', '--dir'], description = 'Base dir') File baseDir,
            @CommandLine.Option(names = ['-v', '--verbose'], description = 'Verbose') boolean verbose,
            @CommandLine.Option(names = ['-a', '--all'], description = 'All') boolean all,
            @CommandLine.Parameters(paramLabel = 'action') String action,
            @CommandLine.Parameters(paramLabel = 'args', description = "args passed to the action") List<String> args
    ) {
        Howto howto = Howto.create(baseDir ?: new File("."))
        howto.verbose = verbose
        howto.all = all

        def val = howto.invoke(action, args)
        if (val < 0) {
            println "Action not found: $action"
            return 1
        }
        val
    }
}
