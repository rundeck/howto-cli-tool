/*
 * This Groovy source file was generated by the Gradle 'init' task.
 */
package us.schueler.cando.app

import groovy.transform.CompileStatic
import groovy.util.logging.Slf4j
import picocli.CommandLine
import us.schueler.cando.utilities.Cando


@CompileStatic
@CommandLine.Command(
        name = "cando",
        version = "0.1",
        mixinStandardHelpOptions = true, // add --help and --version options
        description = """See what you can do and do it""")
@Slf4j
class App {
    static void main(String[] args) {
        if (args.length > 0 && args[0] !in ['-V', '--version', '-h', '--help', 'help', 'ls', 'list', 'run']) {
            //assume a run command is intended
            args = new ArrayList<String>(['run'] + args.toList()).toArray(new String[]{})
        } else if (args.length == 0) {
            args = ['help'].toArray(new String[1])
        }
        System.exit(new CommandLine(new App()).execute(args))
    }


    @CommandLine.Command(name = 'help', aliases = ['list', 'ls'], description = 'List available actions')
    void help(
            @CommandLine.Option(names = ['-d', '--dir'], description = 'Base dir') File baseDir,
            @CommandLine.Option(names = ['-v', '--verbose'], description = 'Verbose') boolean verbose
    ) {
        Cando cando = Cando.create(baseDir ?: new File("."))
        cando.verbose = verbose
        cando.invoke('help', null)
    }

    @CommandLine.Command(name = 'run', description = 'Run an action')
    void run(
            @CommandLine.Option(names = ['-d', '--dir'], description = 'Base dir') File baseDir,
            @CommandLine.Option(names = ['-v', '--verbose'], description = 'Verbose') boolean verbose,
            @CommandLine.Parameters(paramLabel = 'action') String action,
            @CommandLine.Parameters(paramLabel = 'args', description = "args passed to the action") List<String> args
    ) {
        Cando cando = Cando.create(baseDir ?: new File("."))
        cando.verbose = verbose
        cando.invoke(action, args)
    }
}
